WHITESPACE = _{ " " | "\t" | "\r" }

COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

datatype = { "integer" | "float" | "boolean" | (!"datetime" ~ "date") | "time" | "datetime" | "varchar(" ~ (!"0" ~ ASCII_DIGIT) ~ ASCII_DIGIT* ~ ")" }
attribute_prefix = { "attribute" | "id" }
attribute = { attribute_prefix ~ ident ~ ("type" ~ datatype)? } 
entity = { "entity" ~ ident ~ (!"\n\n" ~ "\n" ~ attribute)* }

relation_name = {  (!")" ~ ANY)* }
relation = { "relation" ~ ident ~ ("(" ~ relation_name ~ ")")? ~ ((!"\n\n" ~ "\n") ~ (member | attribute))+ }

member = { cardinality ~ optionality ~ ident }
cardinality = { "multiple" | "one" | ("exactly(" ~ ASCII_DIGIT+ ~ ")" ) }
optionality = { "optional" | "required" }

expression = { entity | relation | entity_table | relation_table }

foreign = { "foreign" ~ "key" ~ ident ~ "for" ~ ident }
table_attribute = _{ foreign }
entity_table = { !relation_table ~ "table" ~ ident ~ "from" ~ "entity" ~ ident ~ (!"\n\n" ~ "\n" ~ table_attribute)* }
relation_table = { "table" ~ ident ~ "from" ~ "relation" ~ ident }

erd = _{ SOI ~ "\n"* ~ expression ~ ("\n"{2,} ~ expression)* ~ "\n"* ~ EOI }

