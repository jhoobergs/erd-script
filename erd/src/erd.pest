WHITESPACE = _{ " " | "\t" | "\r" }

COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

attribute_prefix = { "attribute" | "id" }
attribute = { attribute_prefix ~ ident } 
entity = { "entity" ~ ident ~ (!"\n\n" ~ "\n" ~ attribute)* }

relation_name = {  (!")" ~ ANY)* }
relation = { "relation" ~ ident ~ ("(" ~ relation_name ~ ")")? ~ ((!"\n\n" ~ "\n") ~ (member | attribute))+ }

member = { cardinality ~ optionality ~ ident }
cardinality = { "multiple" | "one" | ("exactly(" ~ ASCII_DIGIT+ ~ ")" ) }
optionality = { "optional" | "required" }

expression = { entity | relation | table }

foreign = { "foreign" ~ "key" ~ ident ~ "for" ~ ident }
table_attribute = _{ foreign }
table = { "table" ~ ident ~ "from" ~ ident ~ (!"\n\n" ~ "\n" ~ table_attribute)* }

erd = _{ SOI ~ "\n"* ~ expression ~ ("\n"{2,} ~ expression)* ~ "\n"* ~ EOI }

